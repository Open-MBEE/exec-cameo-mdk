import java.time.LocalDateTime
import java.time.format.DateTimeFormatter
import java.util.stream.Collectors
import java.util.zip.ZipFile

String buildBranch = project.getProperties().get('buildBranch')
if (buildBranch == null || buildBranch != 'master' && !buildBranch.matches('support/[0-9.]+')) {
    version += '-SNAPSHOT'
}
String buildNumber = project.hasProperty('buildNumber') ? project.getProperty('buildNumber') : System.currentTimeSeconds()
String buildAccess = project.getProperties().getOrDefault('buildAccess', 'external')
String timestamp = DateTimeFormatter.ofPattern("yyyy-MM-dd-HH:mm").format(LocalDateTime.now())
String[] classpathLibraries = ['lib/patch.jar', 'lib/md.jar', 'lib/md_api.jar', 'lib/md_common.jar', 'lib/md_common_api.jar', 'lib/md_common_test.jar', 'lib/tw_common.jar', 'lib/tw_common_api.jar', 'lib/launcher.jar', 'lib/activation.jar', 'lib/annotation.jar', 'lib/batik.jar', 'lib/bundles/ch.qos.logback.classic_1.0.13.jar', 'lib/bundles/ch.qos.logback.core_1.0.13.jar', 'lib/bundles/cmof_1.4.0.201702261832/cmof14.jar', 'lib/bundles/cmof_1.4.0.201702261832/tas.jar', 'lib/bundles/com.esotericsoftware.kryo_2.22.0.201401231130.jar', 'lib/bundles/com.google.gson_2.7.0.jar', 'lib/bundles/com.google.guava_18.0.0.jar', 'lib/bundles/com.nomagic.annotations_1.0.0/com.nomagic.annotation-1.0.jar', 'lib/bundles/com.nomagic.ci.binary_17.0.5.v20170226-1824.jar', 'lib/bundles/com.nomagic.ci.metamodel.project_17.0.5.v20170226-1824.jar', 'lib/bundles/com.nomagic.ci.persistence.local_17.0.5.v20170226-1824.jar', 'lib/bundles/com.nomagic.ci.persistence_17.0.5.v20170226-1824.jar', 'lib/bundles/com.nomagic.ci.services_17.0.5.v20170226-1824.jar', 'lib/bundles/com.nomagic.ci_17.0.5.v20170226-1824.jar', 'lib/bundles/com.nomagic.magicdraw.ce_18.1.0.201702261832.jar', 'lib/bundles/com.nomagic.magicdraw.core.diagram_18.1.0.201702261832.jar', 'lib/bundles/com.nomagic.magicdraw.core.project.options_18.1.0.201702261832.jar', 'lib/bundles/com.nomagic.magicdraw.core_1.0.0.201702261832.jar', 'lib/bundles/com.nomagic.magicdraw.esi.binary.metamodel_18.1.0.201702261832.jar', 'lib/bundles/com.nomagic.magicdraw.esi.esiproject_18.1.0.201702261832.jar', 'lib/bundles/com.nomagic.magicdraw.esi.uuidmap_18.1.0.201702261832.jar', 'lib/bundles/com.nomagic.magicdraw.foundation_2.0.0.201702261832.jar', 'lib/bundles/com.nomagic.magicdraw.osgi.fragment_1.0.0.201702261832.jar', 'lib/bundles/com.nomagic.magicdraw.security_18.3.0.201702261832.jar', 'lib/bundles/com.nomagic.magicdraw.uml2_2.5.0.201702261832.jar', 'lib/bundles/com.nomagic.magicdraw_17.0.5.201702261832.jar', 'lib/bundles/com.typesafe.config_1.3.0.jar', 'lib/bundles/de.schlichtherle.truezip_7.4.3.v20170226-1824/lib/truezip-driver-zip-7.4.3.jar', 'lib/bundles/de.schlichtherle.truezip_7.4.3.v20170226-1824/lib/truezip-kernel-7.4.3.jar', 'lib/bundles/gnu.trove_3.0.3.jar', 'lib/bundles/io.dropwizard.metrics.core_3.1.2.jar', 'lib/bundles/io.netty.buffer_4.0.37.Final.jar', 'lib/bundles/io.netty.codec_4.0.37.Final.jar', 'lib/bundles/io.netty.common_4.0.37.Final.jar', 'lib/bundles/io.netty.handler_4.0.37.Final.jar', 'lib/bundles/io.netty.transport_4.0.37.Final.jar', 'lib/bundles/javax.jmi_1.0.0.201702261832/javax_jmi-1_0-fr.jar', 'lib/bundles/joda-time.source_2.9.1.jar', 'lib/bundles/joda-time_2.9.1.jar', 'lib/bundles/lz4-java_1.3.0.jar', 'lib/bundles/org.apache.commons.codec_1.6.0.v201305230611.jar', 'lib/bundles/org.apache.commons.collections_3.2.1.jar', 'lib/bundles/org.apache.commons.compress_1.3.0.jar', 'lib/bundles/org.apache.commons.io_2.4.0.jar', 'lib/bundles/org.apache.commons.lang_2.6.0.v201404270220.jar', 'lib/bundles/org.apache.directory.api_1.0.0.jar', 'lib/bundles/org.apache.log4j_1.2.15.v201005080500.jar', 'lib/bundles/org.apache.servicemix.bundles.jsr305_2.0.1.1.jar', 'lib/bundles/org.eclipse.core.contenttype_3.4.200.v20140207-1251.jar', 'lib/bundles/org.eclipse.core.jobs_3.6.0.v20140424-0053.jar', 'lib/bundles/org.eclipse.core.runtime_3.10.0.v20140318-2214.jar', 'lib/bundles/org.eclipse.emf.common_2.10.1.v20140901-1043.jar', 'lib/bundles/org.eclipse.emf.ecore.change_2.10.0.v20140901-1043.jar', 'lib/bundles/org.eclipse.emf.ecore.xmi_2.10.1.v20140901-1043.jar', 'lib/bundles/org.eclipse.emf.ecore_2.10.1.v20140901-1043.jar', 'lib/bundles/org.eclipse.emf.edit_2.10.1.v20140901-1055.jar', 'lib/bundles/org.eclipse.equinox.app_1.3.200.v20130910-1609.jar', 'lib/bundles/org.eclipse.equinox.common_3.6.200.v20130402-1505.jar', 'lib/bundles/org.eclipse.equinox.ds_1.4.200.v20131126-2331.jar', 'lib/bundles/org.eclipse.equinox.preferences_3.5.200.v20140224-1527.jar', 'lib/bundles/org.eclipse.equinox.registry_3.5.400.v20140428-1507.jar', 'lib/bundles/org.eclipse.equinox.util_1.0.500.v20130404-1337.jar', 'lib/bundles/org.eclipse.jdt.junit.runtime_3.4.500.v20140527-1138.jar', 'lib/bundles/org.eclipse.jdt.junit4.runtime_1.1.400.v20140314-0036.jar', 'lib/bundles/org.eclipse.osgi.services_3.4.0.v20140312-2051.jar', 'lib/bundles/org.eclipse.osgi_3.10.1.v20140909-1633.jar', 'lib/bundles/org.eclipse.pde.junit.runtime_3.4.400.v20140204-1740.jar', 'lib/bundles/org.eclipse.uml2.common_1.5.0.v200905041045.jar', 'lib/bundles/org.hamcrest.core.source_1.3.0.v201303031735.jar', 'lib/bundles/org.hamcrest.core_1.3.0.v201303031735.jar', 'lib/bundles/org.jboss.netty_3.10.6.Final.jar', 'lib/bundles/org.junit.source_4.11.0.v201303080030.jar', 'lib/bundles/org.junit_4.11.0.v201303080030/junit.jar', 'lib/bundles/org.uncommons.maths_1.2.2.201301151508.jar', 'lib/bundles/slf4j.api_1.7.7.jar', 'lib/com.nomagic.osgi.launcher-17.0.5-SNAPSHOT.jar', 'lib/commons-compress-1.3.jar', 'lib/commons-httpclient-3.1.jar', 'lib/commons-logging-1.0.4.jar', 'lib/concurrent.jar', 'lib/EccpressoAll.jar', 'lib/ehcache-2.10.1.jar', 'lib/flexlm.jar', 'lib/graphics/clibwrapper_jiio.jar', 'lib/graphics/freehep-base.jar', 'lib/graphics/freehep-graphics2d.jar', 'lib/graphics/freehep-graphicsio-emf.jar', 'lib/graphics/freehep-graphicsio-ps.jar', 'lib/graphics/freehep-graphicsio.jar', 'lib/graphics/jai_imageio.jar', 'lib/gson-2.2.4.jar', 'lib/HTMLEditorLight.jar', 'lib/javassist.jar', 'lib/jhall.jar', 'lib/jide-action.jar', 'lib/jide-charts.jar', 'lib/jide-common.jar', 'lib/jide-components.jar', 'lib/jide-dock.jar', 'lib/jide-editor.jar', 'lib/jide-gantt.jar', 'lib/jide-grids.jar', 'lib/jide-shortcut.jar', 'lib/jimi.jar', 'lib/jmyspell-core-1.0.0-beta-2.jar', 'lib/jna.jar', 'lib/jnp-client.jar', 'lib/jtidy-sources.zip', 'lib/jtidy.jar', 'lib/lucene-analyzers-common-4.10.3.jar', 'lib/lucene-core-4.10.3.jar', 'lib/webservice/axis-config.jar', 'lib/webservice/axis.jar', 'lib/webservice/commons-discovery-0.2.jar', 'lib/webservice/jaxrpc.jar', 'lib/webservice/mdserviceclient.jar', 'lib/webservice/rsclient.jar', 'lib/webservice/wsdl4j-1.5.1.jar', 'lib/xalan.jar', 'lib/y.jar']

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.4.0'
        classpath group: 'org.zeroturnaround', name: 'gradle-jrebel-plugin', version: '1.1.3'
    }
}

apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'distribution'
apply plugin: 'rebel'

// In this section you declare where to find the dependencies of your project
repositories {
    jcenter()
    // mavenCentral()
    if (buildAccess == 'internal') {
        maven {
            url 'https://cae-artifactory.jpl.nasa.gov/artifactory/maven-libs-snapshot-virtual'
            credentials {
                username project.getProperties().get('artifactory_user')
                password project.getProperties().get('artifactory_password')
            }
        }
    }
    else {
        ivy {
            // http://d1g91r27pzl568.cloudfront.net/magicdraw185/MagicDraw_185_no_install.zip
            // http://d1g91r27pzl568.cloudfront.net/sysml185/SysML_Plugin_185_bundle.zip
            // http://d1g91r27pzl568.cloudfront.net/cst185/Cameo_Simulation_Toolkit_185.zip
            url 'http://d1g91r27pzl568.cloudfront.net/'
            layout 'pattern', {
                artifact '/[module][revision]/[classifier].[ext]'
            }
        }
    }
}

configurations {
    preCompile
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // Production code requires MagicDraw patches to compile
    if (buildAccess == 'internal') {
        preCompile group: 'gov.nasa.jpl.cae.magicdraw', name: 'cae-magicdraw-core', version: '3.0.0-SNAPSHOT', ext: 'zip'
    }
    else {
        preCompile group: 'com.nomagic', name: 'magicdraw', version: '185', classifier: 'MagicDraw_185_no_install', ext: 'zip'
        preCompile group: 'com.nomagic', name: 'sysml', version: '185', classifier: 'SysML_Plugin_185_bundle', ext: 'zip'
        preCompile group: 'com.nomagic', name: 'cst', version: '185', classifier: 'Cameo_Simulation_Toolkit_185', ext: 'zip'
    }
    
    // SLF4J API
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.2'
    // compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21+'
    
    // SLF4J-Log4j12
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.2'
    // compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.21+'

    // JSoup
    compile group: 'org.jsoup', name: 'jsoup', version: '1.6.1'
    // compile group: 'org.jsoup', name: 'jsoup', version: '1.9.1+'
    
    // JGraphT 
    compile group: 'net.sf.jgrapht', name: 'jgrapht', version: '0.8.3'
    // compile group: 'org.jgrapht', name: 'jgrapht', version: '1.0.0+'
    
    // JSON Simple
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1'
    // compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1+'
    
    // SwingX-All
    compile group: 'org.swinglabs.swingx', name: 'swingx-all', version: '1.6.4'
    // compile group: 'org.swinglabs.swingx', name: 'swingx-all', version: '1.6.5+'
    
    // OpenCSV
    compile group: 'net.sf.opencsv', name: 'opencsv', version: '2.3'
    // compile group: 'com.opencsv', name: 'opencsv', version: '3.8+'
    
    // ActiveMQ 
    compile group: 'org.apache.activemq', name: 'activemq-all', version: '5.9.1'
    // compile group: 'org.apache.activemq', name: 'activemq-all', version: '5.14.0+'

    // Jackson
    compile group: 'com.fasterxml.jackson.core', name:'jackson-databind', version: '2.8.3'

    // JSON Patch
    compile group: "com.box", name: "json-patch", version: "1.13"

    // Apache Commons-IO
    compile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
//    compile group: 'commons-io', name: 'commons-io', version: '2.4'

    // Apache HTTP
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.2'
    compile group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.2'
    
    // Other dependencies we're unable to resolve via repositories
    compile fileTree(dir: 'build/dependency-cache/extracted', include: ['lib/**/*.jar', 'plugins/**/*.jar'], exclude: ['plugins/com.nomagic.collaborator.publisher/**/*.jar', 'plugins/com.nomagic.magicdraw.emfuml2xmi*/**/*.jar'])
    compile fileTree(dir: 'lib', include: ['**/*.jar'])
    
	// Test Dependencies
    //testCompile 'junit:junit:4.11'
    testCompile group: 'org.gradle', name: 'gradle-core', version: '3.1'
}

task extractDependencies {
    configurations.preCompile.resolvedConfiguration.resolvedArtifacts.each { artifact ->
        inputs.file artifact.file
        def zipFile = new ZipFile(artifact.file)
        zipFile.entries().each { entry ->
            if (!entry.isDirectory()) {
                outputs.file 'build/dependency-cache/extracted/' + entry.getName()
            }
        }
    }
    doLast {
        inputs.files.each { archive ->
            copy {
                from zipTree(archive)
                into 'build/dependency-cache/extracted'
            }
        }
        fileTree(dir: 'build/dependency-cache/extracted', include: '*.zip').each { archive ->
            copy {
                from zipTree(archive)
                into 'build/dependency-cache/extracted'
            }
        }
        /*fileTree(dir: 'build/dependency-cache/extracted').each { file ->
            outputs.file file
        }*/
    }
}
compileJava.dependsOn extractDependencies

class PreDist extends DefaultTask {
    String humanVersion
    String internalVersion
    String buildTimestamp
    String descriptor

    @TaskAction
    void executeTask() {
        project.copy {
            from 'src/main/dist'
            exclude 'template'
            from 'src/main/dist/template'
            into 'build/generated-dist'
        }
        project.copy {
            from project.configurations.compile.resolvedConfiguration.resolvedArtifacts.file
            from 'lib'
            from project.jar
            into "build/generated-dist/plugins/${project.group}"
        }

        String pluginLibraries = ''
        project.fileTree(dir: "build/generated-dist/plugins/${project.group}", include: '**/*.jar').each { file ->
            pluginLibraries += '\t\t<library name=\"' + file.absolutePath.replaceAll(~/.*build\/generated-dist\/plugins\/${project.group}\//, '') + '\"/>' + System.lineSeparator()
        }
        project.copy {
            from "src/main/dist/template/plugins/${project.group}/plugin.xml"
            filter { String line -> line.replace('<!-- START AUTO-GENERATED -->', '<!-- START AUTO-GENERATED -->' + System.lineSeparator() + System.lineSeparator() + pluginLibraries) }
            filter { String line -> line.replace('${human.version}', humanVersion) }
            filter { String line -> line.replace('${internal.version}', internalVersion) }
            into "build/generated-dist/plugins/${project.group}"
        }

        String descriptorFiles = ''
        project.fileTree(dir: 'build/generated-dist').each { file ->
            String relativePath = file.absolutePath.replaceAll(/.*build\/generated-dist\//, '')
            descriptorFiles += '\t\t<file from=\"' + relativePath + '\" to=\"' + relativePath + '\"/>' + System.lineSeparator()
        }
        project.copy {
            from "src/main/dist/template/data/resourcemanager/${descriptor}"
            filter { String line -> line.replace('<!-- START AUTO-GENERATED -->', '<!-- START AUTO-GENERATED -->' + System.lineSeparator() + System.lineSeparator() + descriptorFiles) }
            filter { String line -> line.replace('${human.version}', humanVersion) }
            filter { String line -> line.replace('${internal.version}', internalVersion) }
            filter { String line -> line.replace('${build.timestamp}', buildTimestamp) }
            into 'build/generated-dist/data/resourcemanager'
        }
    }
}

task preDist(type: PreDist, dependsOn: jar) {
    humanVersion = version
    internalVersion = buildNumber
    buildTimestamp = timestamp
    descriptor = descriptorFile
}

distributions {
    main {
        contents {
            exclude 'template'
            from 'build/generated-dist'
        }
    }
}

assembleDist.dependsOn.remove distTar
tasks.remove(distTar)

distZip {
    includeEmptyDirs false
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    eachFile { file ->
        file.setPath(path.substring(path.indexOf("/") + 1, path.length()))
    }
}

distZip.dependsOn preDist

javadoc {
    options.addStringOption("sourcepath", "")
}

if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        // disable the crazy super-strict doclint tool in Java 8
        //noinspection SpellCheckingInspection
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task testsJar(type: Jar, dependsOn: testClasses) {
    classifier = 'tests'
    from sourceSets.test.output

    exclude 'gov/nasa/jpl/mbee/mdk/test/framework/**'
    exclude 'org/**'
    exclude 'worker/**'
    //include 'gov/nasa/jpl/mbee/mdk/test/tests/**'
}

task testsHackJar(type: Jar, dependsOn: testClasses) {
    classifier = 'tests-hack'
    from sourceSets.test.output

    include 'gov/nasa/jpl/mbee/mdk/test/framework/**'
    include 'org/**'
    include 'worker/**'
    //exclude 'gov/nasa/jpl/mbee/mdk/test/tests/**'

    manifest {
        attributes(
            'Main-Class': 'gov.nasa.jpl.mbee.mdk.test.framework.GradleMagicDrawLauncher'
        )
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

if (buildAccess == 'internal') {
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.artifactory'

    publishing {
        // Reference to Gradle publications defined in the build script.
        // This is how we tell the Artifactory Plugin which artifacts should be
        // published to Artifactory.
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar {
                    classifier "sources"
                }
                artifact testsJar {
                    classifier "tests"
                }
                artifact javadocJar {
                    classifier "javadoc"
                }
                artifact distZip {
                    classifier "plugin"
                }
            }
        }
    }

    artifactory {
        contextUrl = 'https://cae-artifactory.jpl.nasa.gov/artifactory/'
        publish {
            repository {
                repoKey = 'maven-plugins-' + (version.endsWith('SNAPSHOT') ? 'snapshot' : 'release') + '-local' // The Artifactory repository key to publish to
                username = project.getProperties().get('artifactory_user') // The publisher user name
                password = project.getProperties().get('artifactory_password') // The publisher password
                maven = true
            }
            defaults {
                publications('mavenJava')
                publishArtifacts = true
                // Properties to be attached to the published artifacts.
                //properties = ['qa.level': 'basic', 'dev.team' : 'core']
                // Publish generated POM files to Artifactory (true by default)
                publishPom = true
            }
        }
    }

    artifactoryPublish.dependsOn distZip
}

installDist {
    destinationDir = file('build/plugin')
    doLast {
        copy {
            fileMode 0755
            from 'build/dependency-cache/extracted'
            from 'build/plugin'
            into 'build/install'
        }
    }
}

installDist.dependsOn distZip

task runJava(type: JavaExec) {
    doFirst {
        classpath fileTree(dir: 'build/install', include: classpathLibraries)
    }
    workingDir 'build/install'
    ignoreExitValue true

    standardOutput = System.out
    errorOutput = System.err

    main = 'com.nomagic.osgi.launcher.ProductionFrameworkLauncher'
    jvmArgs = ['-Xmx8192M', '-Xss512M', '-DLOCALCONFIG=true', '-DWINCONFIG=true', '-Djsse.enableSNIExtension=false', '-Djava.net.preferIPv4Stack=true', '-Dcom.sun.media.imageio.disableCodecLib=true', '-noverify', '-Dlocal.config.dir.ext=-dev', '-splash:data/splash.png', '-Dmd.class.path=$java.class.path', '-Dcom.nomagic.osgi.config.dir=configuration', '-Desi.system.config=data/application.conf', '-Dlogback.configurationFile=data/logback.xml', '-Dsun.locale.formatasdefault=true']
    // arguments to pass to the application
    args 'DEVELOPER'
}

runJava.dependsOn installDist
runJava.dependsOn generateRebel

task testAVG(type: JavaExec) {
    doFirst {
        classpath fileTree(dir: 'build/install', include: classpathLibraries)
    }
    workingDir 'build/install'
    ignoreExitValue true

    standardOutput = System.out
    errorOutput = System.err

    main = 'com.nomagic.osgi.launcher.ProductionFrameworkLauncher'
    jvmArgs = ['-Xmx4000M', '-Xss1024M', '-DLOCALCONFIG=true', '-DWINCONFIG=true', '-Djsse.enableSNIExtension=false', '-Djava.net.preferIPv4Stack=true', '-Dcom.sun.media.imageio.disableCodecLib=true', '-noverify', '-Dlocal.config.dir.ext=-dev', '-splash:data/splash.png', '-Dmd.class.path=$java.class.path', '-Dcom.nomagic.osgi.config.dir=configuration', '-Desi.system.config=data/application.conf', '-Dlogback.configurationFile=data/logback.xml', '-Dsun.locale.formatasdefault=true', '-Dcom.nomagic.magicdraw.launcher=gov.nasa.jpl.mbee.pma.analyses.AutomatedViewGeneration']
    // arguments to pass to the application
    args '-twprj', 'ID_10_20_16_12_01_59_AM__58f6714a_157e0e07e65__7ffd_LMC_055004_192_168_1_27', '-doclist', '_18_0_2_bec02f9_1444944948911_779834_74342', '-crdlc', '../../resources/mms.properties', '-twbrn', 'Branch2'
}

testAVG.dependsOn installDist
testAVG.dependsOn generateRebel



task runScript(type: Exec) {
    workingDir "build/install"
    ignoreExitValue = true

    if (System.getProperty('os.name').toLowerCase().contains('windows')) {
        commandLine 'bin\\magicdraw.exe'
    } else {
        commandLine 'sh', 'bin/magicdraw'
    }
}

runScript.dependsOn installDist

test {
    ignoreFailures = true
    testLogging.showStandardStreams = true
    forkEvery 1

    workingDir 'build/install'

    /**
     * The short version of the rationale is that with 18.4, MagicDraw introduced the OSGi framework as part of its
     * implementation.
     * Gradle and JUnit work very well together out of the box (generated reports in XML and HTML, tight IDE/Jenkins
     * integration, etc.), and Gradle and OSGi was made to work with the runJava task.
     * However, Gradle had to implement a number of hacks to overcome OS limitations and achieve features that can be
     * applied to a broad range of software.
     * Those hacks work well with most applications but not OSGi (commentary: OSGi doesn't play well with anyone), so
     * we implemented a number of hacks ourselves.
     * They involve classloading order, classpath redirection using the jar argument, reflection, and dynamic
     * classloading.
     * Ultimately, this enabled the use of the well-supported and feature-rich Gradle test task, but at the cost of
     * an implementation that could break in the unlikely, albeit possible, case of either Gradle or MagicDraw
     * significantly changing their implementations of the test and OSGi frameworks, respectively.
     *
     * Clears the default classpath since all the necessary libraries are being passed as arguments to
     * GradleMagicDrawLauncher to be added to the OSGi classpath.
     * Then it utilizes the -jar JVM argument to override the default Gradle command and pass the original command
     * as arguments to the main class of the jar, GradleMagicDrawLauncher,
     * including the classpath arguments (one of our own and one of Gradle's). It additionally parses and sets the
     * system properties that are passed.
     *
     * Example command: .../java -Dcom.nomagic.osgi.config.dir=configuration -Desi.system.config=data/application.conf -Djava.security.manager=worker.org.gradle.process.internal.worker.child.BootstrapSecurityManager -Dlogback.configurationFile=data/logback.xml -Dmd.class.path=$java.class.path -jar /Users/igomes/mdk/build/libs/mdk-*-tests-hack.jar -cp ... -Dfile.encoding=UTF-8 -Duser.country=US -Duser.language=en -Duser.variant -ea -cp .../.gradle/caches/.../workerMain/gradle-worker.jar worker.org.gradle.process.internal.worker.GradleWorkerMain 'Gradle Test Executor 1'
     *
     * @author igomes
     */
    classpath = files()
    doFirst {
        def arguments = ['-jar', testsHackJar.outputs.files.singleFile, '-cp',
                         fileTree(dir: 'build/generated-dist', include: '**/*.jar').getFiles().stream().map { i -> i.getAbsolutePath() }.collect(Collectors.joining(File.pathSeparator)) + File.pathSeparator +
                                 /*fileTree(dir: 'build/install', include: ['plugins/** /*.jar']).asPath + */
                                 fileTree(dir: 'build/install', include: classpathLibraries).getFiles().stream().map { i -> i.getAbsolutePath() }.collect(Collectors.joining(File.pathSeparator)) + File.pathSeparator +
                                 testsJar.outputs.files.singleFile, '-DLOCALCONFIG=true', '-DWINCONFIG=true', '-Djsse.enableSNIExtension=false', '-Djava.net.preferIPv4Stack=true', '-Dcom.sun.media.imageio.disableCodecLib=true', '-noverify', '-Dlocal.config.dir.ext=-test', '-splash:data/splash.png', '-Dmd.class.path=$java.class.path', '-Dcom.nomagic.osgi.config.dir=configuration', '-Desi.system.config=data/application.conf', '-Dlogback.configurationFile=data/logback.xml', '-Dsun.locale.formatasdefault=true']
        if (buildAccess == 'internal') {
            arguments.addAll(['-DFL_FORCE_USAGE=true', '-DFL_SERVER_ADDRESS=cae-lic04.jpl.nasa.gov', '-DFL_SERVER_PORT=1101', '-DFL_EDITION=18.5'])
        }
        jvmArgs = arguments
    }
}

test.dependsOn testsJar
test.dependsOn testsHackJar
test.dependsOn installDist